Looking for clear text passwords in the username field. It helps to have a standard name patten. The sub search is looking for a deviation. Date_Time is a macro to
convert time to a format you prefer. So you will have to make the macro or use the convert time command there. 

sourcetype="WinEventLog:Security" EventCode=4768
[search sourcetype="WinEventLog:Security" EventCode=4768 action="failure"
    | eval userlen = len(Account_Name)
    | where userlen >= 8
    | regex Account_Name="[[:digit:]]|[\s\!\@\#\$\%\^\&\*\(\)]"
    | regex Account_Name!="^<Do not look for this sub search>$"
    | table clientip ]
| `Date_Time`
| regex Account_Name!="^<Do not look for this main search>$"
| eval ru=case(action=="success", Account_Name)
| eval pp=case(action=="failure", Account_Name)
| stats min(Date_Time) AS min_time max(Date_Time) AS max_time values(Account_Name) AS User_Submitted values(ru) AS Real_User values(pp) AS Possible_Password by clientip
| eval Time = min_time + " - " + max_time
| rename clientip AS Client_IP
| table Time Client_IP Real_User User_Submitted Possible_Password
