Looking for clear text passwords in the username field of Pulse VPN logs. It helps to have a standard username patten. The sub search is looking for a deviation. Date_Time is a macro to
convert time to a format you prefer. So you will have to make the macro or use the convert time command there. 

sourcetype=network_syslog "PulseSecure"
[search sourcetype=network_syslog "PulseSecure" "Login failed using auth server" OR "Primary authentication failed" 
| eval userlen = len(UserName)
| regex UserName!="^<Do not look for this sub search>$"
| where userlen >= 8 
| regex UserName="[[:digit:]]|[\s\!\@\#\$\%\^\&\*\(\)]"
| table clientip]
| eval unl=len(UserName)
| eval pp=case(unl >= 8, UserName)
| `Date_Time`
| stats min(Date_Time) AS min_time max(Date_Time) AS max_time values(pp) AS Possible_Password values(UserName) AS UserAcct by clientip 
| eval Time = min_time + " - " + max_time
| table Time clientip Possible_Password UserAcct
